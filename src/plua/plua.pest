Script = { SOI ~ Block ~ EOI }


Block = { (Stat ~ ";"?)* }


Stat = { Assignment | Do | While }

    Assignment = { VarList1 ~ "="   ~ ExpList1 }
    Do         = { "do"     ~ Block ~ "end"    }
    While      = { "while"  ~ Exp   ~ "do"     ~ Block ~ "end" }

    VarList1 = _{ Var ~ ("," ~ Var)* }
    ExpList1 = _{ Exp ~ ("," ~ Exp)* }


Exp = { Term ~ (Op ~ Term)* }

    Term = _{ Atom | "(" ~ Exp ~ ")" }
    Atom = _{ Val  | Var             }


Var = { Name }


Val = { Nil  | Bool | Num }

    Nil  = @{ "nil" }
    Bool =  { True  | False }
    Num  = _{ Float | Int   }

    True  = @{ "true"  }
    False = @{ "false" }


Op = { OpArith | OpComp | OpLogic }

    OpArith = _{ Pow | Mul | Div | Add | Sub }
    OpComp  = _{ Lt  | Leq | Gt  | Geq | Eq  | Neq }
    OpLogic = _{ Not | And | Or  }

    Not = { "not" }

    Pow = { "^"   }

    Mul = { "*"   }
    Div = { "/"   }

    Add = { "+"   }
    Sub = { "-"   }

    Lt  = { "<"   }
    Leq = { "<="  }
    Gt  = { ">"   }
    Geq = { ">="  }

    Eq  = { "=="  }
    Neq = { "~="  }

    And = { "and" }

    Or  = { "or"  }


Name = @{ !Keyword ~ NameSeq }

    Keyword = _{ "do" | "while" | "end" }

    NameSeq = _{ (Lower | "_") ~ (Alpha | Digit | "_")* }


Float = @{ NumSign? ~ Int    ~ "." ~ Digit+ }
Int   = @{ NumSign? ~ Digit+ }

    NumSign = _{ ("+" | "-") }


Lower = _{ 'a' .. 'z' }
Upper = _{ 'A' .. 'Z' }
Alpha = _{ Lower | Upper }
Digit = _{ '0' .. '9' }


WHITESPACE = _{ " " | "\t" | Newline }

    Newline = _{ "\n" | "\r\n" }


COMMENT = _{ BlockComment | ("--" ~ (!Newline ~ ANY)*) }

    BlockComment = _{ "--[[" ~ (BlockComment | !"]]" ~ ANY)* ~ "]]" }
